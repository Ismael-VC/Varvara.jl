( Copyright (c) 2021
  https://github.com/Ismael-VC/Varvara.jl/blob/main/CONTRIBUTORS.md

  Permission to use, copy, modify, and distribute this software for any
  purpose with or without fee is hereby granted, provided that the above
  copyright notice and this permission notice appear in all copies.

  THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
  WITH REGARD TO THIS SOFTWARE. )

( 
	An integer greater than one is called a prime number 
	if its only positive divisors are one and itself. )

%RTN { JMP2r }
%HALT { #0101 #0e DEO2 }
%MOD2 { DIV2k MUL2 SUB2 }
%EMIT { #18 DEO }

|0100 ( -> ) @main

	#0000 #0001
	&loop
		DUP2 ,is-prime JSR #00 EQU ,&skip JCN
			DUP2 ,print-hex/short JSR
			#20 EMIT
			&skip
		INC2 NEQ2k ,&loop JCN
	POP2 POP2
	HALT
	
BRK

@is-prime ( number* -- flag )

	DUP2 #0001 NEQ2 ,&not-one JCN
		POP2 #00 RTN
		&not-one
	STH2k
	( range ) #01 SFT2 #0002
	&loop
		STH2kr OVR2 MOD2 #0000 NEQ2 ,&continue JCN
			POP2 POP2 
			POP2r #00 RTN
			&continue
		INC2 GTH2k ,&loop JCN
	POP2 POP2 
	POP2r #01

RTN

@print-hex ( value* -- )
	
	&short ( value* -- )
		SWP ,&echo JSR 
	&byte ( value -- )
		,&echo JSR
	RTN

	&echo ( value -- )
	STHk #04 SFT ,&parse JSR EMIT
	STHr #0f AND ,&parse JSR EMIT
	RTN
	&parse ( value -- char )
		DUP #09 GTH ,&above JCN #30 ADD RTN &above #09 SUB #60 ADD RTN

RTN
