( drum rack )

%+  { ADD } %-   { SUB }  %*  { MUL } %/   { DIV }
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ }
%++ { ADD2 } %-- { SUB2 } %** { MUL2 } %// { DIV2 }
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }

%2** { #10 SFT2 } %2// { #01 SFT2 }
%4** { #20 SFT2 } %4// { #02 SFT2 }
%8** { #30 SFT2 } %8// { #03 SFT2 }
%10** { #40 SFT2 }

%TOB { NIP } %TOS { #00 SWP }
%RTN { JMP2r }
%MOD { DUP2 / * - }
%LTS2  { #8000 ++ SWP2 #8000 ++ >> } %GTS2  { #8000 ++ SWP2 #8000 ++ << }

%PAD-WIDTH { #0030 }
%PAD-HEIGHT { #0020 }

( devices )

|00 @System     [ &vector $2 &pad      $6 &r      $2 &g     $2 &b      $2 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 ]
|20 @Screen     [ &vector $2 &width    $2 &height $2 &pad   $2 &x      $2 &y     $2 &addr   $2 &pixel  $1 &sprite $1 ]
|30 @Audio0     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|40 @Audio1     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|50 @Audio2     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|60 @Audio3     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|70 @Midi       [ &vector $2 &channel  $1 &note   $1 &velocity $1 ]
|80 @Controller [ &vector $2 &button   $1 &key    $1 ]
|90 @Mouse      [ &vector $2 &x        $2 &y      $2 &state $1 &wheel $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset-hs $2 &offset-ls $2 &name $2 &length $2 &load $2 &save $2 ]

( variables )

|0000

@center   [ &x $2 &y $2 ]
@pointer  [ &x $2 &y $2 ]
@piano    [ &last $1 &octave $1 ]
@pads     [ &last $1 ]
@frame    [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@pad-view [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@mix-view [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@oct-view [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@wav-view [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]

( TODO: remove )
@color $1
@rect        [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]

@modes [ $1 $1 $1 $1 ] ( single/repeat )

( program )

|0100 ( -> )
	
	( theme ) 
	#0fe5 .System/r DEO2 
	#0fc5 .System/g DEO2 
	#0f25 .System/b DEO2

	( vectors )
	;on-control .Controller/vector DEO2
	;on-mouse   .Mouse/vector DEO2
	;on-frame   .Screen/vector DEO2
	;on-message .Console/vector DEO2

	( channel defaults )
	#dd   .Audio0/volume DEO 
	#0118 .Audio0/adsr DEO2
	#0200 .Audio0/length DEO2
	#ce   .Audio1/volume DEO 
	#0334 .Audio1/adsr DEO2
	#0800 .Audio1/length DEO2
	#ec   .Audio2/volume DEO 
	#0414 .Audio2/adsr DEO2
	#0800 .Audio2/length DEO2
	#ee   .Audio3/volume DEO 
	#022c .Audio3/adsr DEO2
	#1000 .Audio3/length DEO2

	( defaults )
	#01 .modes STZ
	#01 .piano/octave STZ

	( find center )
	.Screen/width DEI2 2// .center/x STZ2
	.Screen/height DEI2 2// .center/y STZ2

	( place frame )
	.center/x LDZ2 PAD-WIDTH #0003 ** -- #0010 -- .frame/x1 STZ2
	.center/y LDZ2 #0050 -- .frame/y1 STZ2
	.center/x LDZ2 PAD-WIDTH #0003 ** ++ #0010 ++ .frame/x2 STZ2
	.frame/y1 LDZ2 PAD-HEIGHT 4** ++ #0028 ++ .frame/y2 STZ2

	( place pad-view )
	.frame/x1 LDZ2 .pad-view/x1 STZ2
	.frame/y1 LDZ2 .pad-view/y1 STZ2
	.pad-view/x1 LDZ2 #0004 PAD-WIDTH ** ++ .pad-view/x2 STZ2
	.pad-view/y1 LDZ2 #0004 PAD-HEIGHT ** ++ .pad-view/y2 STZ2

	( place mix-view )
	.pad-view/x2 LDZ2 #0006 ++ .mix-view/x1 STZ2
	.pad-view/y1 LDZ2 .mix-view/y1 STZ2
	.mix-view/x1 LDZ2 #007a ++ .mix-view/x2 STZ2
	.pad-view/y2 LDZ2 .mix-view/y2 STZ2

	( place oct-view )
	.frame/x1 LDZ2 .oct-view/x1 STZ2
	.mix-view/y2 LDZ2 #0004 ++ .oct-view/y1 STZ2
	.oct-view/x1 LDZ2 #0050 ++ .oct-view/x2 STZ2
	.oct-view/y1 LDZ2 #0018 ++ .oct-view/y2 STZ2

	( place wav-view )
	.oct-view/x2 LDZ2 .wav-view/x1 STZ2
	.mix-view/y2 LDZ2 #0004 ++ .wav-view/y1 STZ2
	.frame/x2 LDZ2 .wav-view/x2 STZ2
	.wav-view/y1 LDZ2 #0020 ++ .wav-view/y2 STZ2

	( draw outlines )
	.mix-view/x1 LDZ2 #0004 -- 
	.mix-view/y1 LDZ2 #0000 -- 
	.mix-view/x2 LDZ2 #0002 --
	.mix-view/y2 LDZ2 #0002 --
	#01 ;line-rect JSR2
	.wav-view/x1 LDZ2 #0000 -- 
	.wav-view/y1 LDZ2 #0002 -- 
	.wav-view/x2 LDZ2 #0002 --
	.wav-view/y2 LDZ2 #0002 ++
	#01 ;line-rect JSR2

	( initial draw )
	#00 #10
	&draw-pads
		( load sample )
		OVR TOS #0019 ** ;pad-path ++ .File/name DEO2
		OVR #04 / #40 SFT .Audio0/length + DEI2 .File/length DEO2
		OVR TOS 2** ;pad-addr ++ LDA2 .File/load DEO2
		( draw pad )
		OVR #01 ;draw-pad JSR2
		( draw mixer )
		OVR #04 / ;draw-mixer JSR2
		( incr ) SWP INC SWP
		LTHk ,&draw-pads JCN
	POP2

	;draw-octave JSR2

BRK

@on-message ( -> )
	
	.Console/read DEI #0f AND ;play-pad JSR2

BRK

@on-frame ( -> )
	
	( update VU monitors )
	#00 #04
	&loop
		OVR STH
		( x ) .mix-view/x1 LDZ2 #0074 ++
		( y ) .mix-view/y1 LDZ2 PAD-HEIGHT STHkr TOS ** ++ #0002 ++
		( output ) .Audio0/output STHr #40 SFT + DEI 
		;draw-monitor JSR2
		( incr ) SWP INC SWP
		LTHk ,&loop JCN
	POP2

BRK

@on-mouse ( -> )

	;draw-cursor JSR2 

	.Mouse/state DEI #00 ! #01 JCN [ BRK ]

	.Mouse/x DEI2 DUP2 .pad-view/x1 LDZ2 >> ROT ROT .pad-view/x2 LDZ2 INC2 << #0101 ==
	.Mouse/y DEI2 DUP2 .pad-view/y1 LDZ2 >> ROT ROT .pad-view/y2 LDZ2 << #0101 ==
	#0101 == ;on-touch-pad JCN2

	.Mouse/x DEI2 DUP2 .mix-view/x1 LDZ2 >> ROT ROT .mix-view/x2 LDZ2 INC2 << #0101 ==
	.Mouse/y DEI2 DUP2 .mix-view/y1 LDZ2 >> ROT ROT .mix-view/y2 LDZ2 << #0101 ==
	#0101 == ;on-touch-mix JCN2

	.Mouse/x DEI2 DUP2 .oct-view/x1 LDZ2 >> ROT ROT .oct-view/x2 LDZ2 INC2 << #0101 ==
	.Mouse/y DEI2 DUP2 .oct-view/y1 LDZ2 >> ROT ROT .oct-view/y2 LDZ2 << #0101 ==
	#0101 == ;on-touch-oct JCN2

BRK

@on-control ( -> )

	.Controller/key DEI #00 ! #01 JCN [ BRK ]

	.Controller/key DEI 
	DUP #31 ! ,&no-0 JCN #00 ;play-pad JSR2 &no-0
	DUP #32 ! ,&no-1 JCN #01 ;play-pad JSR2 &no-1
	DUP #33 ! ,&no-2 JCN #02 ;play-pad JSR2 &no-2
	DUP #34 ! ,&no-3 JCN #03 ;play-pad JSR2 &no-3
	DUP #71 ! ,&no-4 JCN #04 ;play-pad JSR2 &no-4
	DUP #77 ! ,&no-5 JCN #05 ;play-pad JSR2 &no-5
	DUP #65 ! ,&no-6 JCN #06 ;play-pad JSR2 &no-6
	DUP #72 ! ,&no-7 JCN #07 ;play-pad JSR2 &no-7
	DUP #61 ! ,&no-8 JCN #08 ;play-pad JSR2 &no-8
	DUP #73 ! ,&no-9 JCN #09 ;play-pad JSR2 &no-9
	DUP #64 ! ,&no-a JCN #0a ;play-pad JSR2 &no-a
	DUP #66 ! ,&no-b JCN #0b ;play-pad JSR2 &no-b
	DUP #7a ! ,&no-c JCN #0c ;play-pad JSR2 &no-c
	DUP #78 ! ,&no-d JCN #0d ;play-pad JSR2 &no-d
	DUP #63 ! ,&no-e JCN #0e ;play-pad JSR2 &no-e
	DUP #76 ! ,&no-f JCN #0f ;play-pad JSR2 &no-f
	POP

BRK

@on-touch-pad ( -> )
	
	( x ) .Mouse/x DEI2 .pad-view/x1 LDZ2 -- PAD-WIDTH // TOB
	( y ) .Mouse/y DEI2 .pad-view/y1 LDZ2 -- PAD-HEIGHT // TOB #20 SFT +
	
	DUP SWP ;play-pad JSR2

	;draw-waveform JSR2

	( release ) #00 .Mouse/state DEO

BRK

@on-touch-mix ( -> )

	( channel ) .Mouse/y DEI2 .mix-view/y1 LDZ2 -- PAD-HEIGHT // TOB STH
	( knob ) .Mouse/x DEI2 .mix-view/x1 LDZ2 -- #0010 // TOB
	( adsr )
	DUP #00 ! ,&no-a JCN
		.Audio0/adsr [ STHkr #40 SFT + ] DEI
		#10 .Mouse/state DEI #10 = #e0 * + +
		.Audio0/adsr [ STHkr #40 SFT + ] DEO &no-a
	DUP #01 ! ,&no-d JCN
		.Audio0/adsr [ STHkr #40 SFT + ] DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 = #0e * + + #0f AND STHr +
		.Audio0/adsr [ STHkr #40 SFT + ] DEO &no-d
	DUP #02 ! ,&no-s JCN
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEI
		#10 .Mouse/state DEI #10 = #e0 * + +
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEO &no-s
	DUP #03 ! ,&no-r JCN
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 = #0e * + + #0f AND STHr +
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEO &no-r
	( modes )
	DUP #04 ! ,&no-repeat JCN
		.modes [ STHkr + ] LDZ 
		#00 =
		.modes [ STHkr + ] STZ &no-repeat
	( volume )
	DUP #05 ! ,&no-left JCN
		.Audio0/volume [ STHkr #40 SFT + ] DEI 
		#10 .Mouse/state DEI #10 = #e0 * + +
		.Audio0/volume [ STHkr #40 SFT + ] DEO &no-left
	DUP #06 ! ,&no-right JCN
		.Audio0/volume [ STHkr #40 SFT + ] DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 = #0e * + + #0f AND STHr +
		.Audio0/volume [ STHkr #40 SFT + ] DEO &no-right
	POP

	( release ) #00 .Mouse/state DEO

	STHkr ;draw-mixer JSR2

	POPr

BRK

@on-touch-oct ( -> )
	
	.Mouse/x DEI2 .oct-view/x1 LDZ2 -- 8// TOB #08 ! ,&no-mod JCN
		.Mouse/y DEI2 .oct-view/y1 LDZ2 -- 8// TOB 
		DUP #00 ! ,&no-incr JCN
			.piano/octave LDZ INC .piano/octave STZ &no-incr
		DUP #02 ! ,&no-decr JCN
			.piano/octave LDZ #01 - .piano/octave STZ &no-decr
		POP
		( release ) #00 .Mouse/state DEO
		;draw-octave JSR2
		BRK
	&no-mod

	.Mouse/x DEI2 .oct-view/x1 LDZ2 -- 8// TOB #06 GTH ,&no-key JCN
		.Mouse/x DEI2 .oct-view/x1 LDZ2 -- 8// 
		( set pitch of pad )
		DUP2 ;notes ++ LDA .pads/last LDZ SWP .piano/octave LDZ #02 - #0c * + 
		( save ) SWP TOS ;pad-pitch ++ STA 
		( record last note )
		TOB .piano/last STZ 
		.pads/last LDZ ;play-pad JSR2
		( release ) #00 .Mouse/state DEO
		;draw-octave JSR2
	&no-key

BRK

@play-pad ( pad -- )

	( unselect last )
	.pads/last LDZ #01 ;draw-pad JSR2
	DUP .pads/last STZ 
	( highlight )
	DUP #02 ;draw-pad JSR2
	( addr )
	DUP TOS 2** ;pad-addr ++ LDA2 STH2
	DUP #04 / #40 SFT .Audio0/addr + STH2r ROT DEO2
	( pitch )
	DUP TOS ;pad-pitch ++ LDA STH 
	DUP #04 / .modes + LDZ #00 = #70 SFT STH ADDr
	DUP #04 / #40 SFT .Audio0/pitch + STHr SWP DEO
	#04 / ;draw-mixer JSR2

RTN

@draw-waveform ( pad -- )
	
	( stash address )
	DUP ADD #00 SWP ;pad-addr ++ LDA2 STH2

	( clear )
	.wav-view/x1 LDZ2 #0002 ++
	.wav-view/y1 LDZ2
	.wav-view/x2 LDZ2 #0002 --
	.wav-view/y2 LDZ2
	#00 ;fill-rect JSR2

	.wav-view/x1 LDZ2 #0002 ++ .Screen/x DEO2

	( waveform )
	#00 #e8
	&loop
		( dotted line )
		OVR #01 AND ,&no-dot JCN 
			.wav-view/y1 LDZ2 #0010 ++ .Screen/y DEO2
			#01 .Screen/pixel DEO
		&no-dot
		OVR TOS 10** [ DUP2r STH2r ] ++ LDA 
		#02 /
		TOS 4// .wav-view/y1 LDZ2 ++ .Screen/y DEO2
		.Screen/x DEI2 INC2 .Screen/x DEO2
		( draw ) #02 .Screen/pixel DEO
		( incr ) SWP INC SWP
		LTHk ,&loop JCN
	POP2
	POP2r

RTN

@draw-mixer ( mixer -- )

	STHk #00 SWP PAD-HEIGHT ** .mix-view/y1 LDZ2 ++ #0003 ++

	( adsr )
	DUP2 .mix-view/x1 LDZ2 SWP2
		.Audio0/adsr [ STHkr #40 SFT + ] DEI #04 SFT ;draw-knob JSR2
	DUP2 .mix-view/x1 LDZ2 #0010 ++ SWP2
		.Audio0/adsr [ STHkr #40 SFT + ] DEI #0f AND ;draw-knob JSR2
	DUP2 .mix-view/x1 LDZ2 #0020 ++ SWP2
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEI #04 SFT ;draw-knob JSR2
	DUP2 .mix-view/x1 LDZ2 #0030 ++ SWP2
		.Audio0/adsr [ STHkr #40 SFT + ] INC DEI #0f AND ;draw-knob JSR2
	( once/repeat )
	DUP2 .mix-view/x1 LDZ2 #0040 ++ SWP2
		.modes [ STHkr + ] LDZ ;draw-switch JSR2
	( volume )
	DUP2 .mix-view/x1 LDZ2 #0050 ++ SWP2
		.Audio0/volume [ STHkr #40 SFT + ] DEI #04 SFT ;draw-knob JSR2
	DUP2 .mix-view/x1 LDZ2 #0060 ++ SWP2
		.Audio0/volume [ STHkr #40 SFT + ] DEI #0f AND ;draw-knob JSR2
	POP2
	POPr

RTN

@draw-pad ( pad color -- )
	
	STH STH

	STHkr 

	DUP #04 / SWP #03 AND TOS PAD-WIDTH ** ( center ) .pad-view/x1 LDZ2 ++ 
	ROT TOS PAD-HEIGHT ** ( center ) .pad-view/y1 LDZ2 ++ 

	( draw outline )
	OVR2 OVR2 
	OVR2 PAD-WIDTH #0002 -- ++ 
	OVR2 PAD-HEIGHT #0002 -- ++ 
	OVRr STHr ;line-rect JSR2

	( draw name )
	OVR2 #0002 ++ 
	OVR2 #0002 ++ 
	;pad-name STHkr TOS #0005 ** ++ 
	#00 OVRr STHr + 
	;draw-label JSR2

	( draw note )
	OVR2 #0003 ++ .Screen/x DEO2
	DUP2 #0014 ++ .Screen/y DEO2
	( get pitch ) STHkr TOS ;pad-pitch ++ LDA
	#0c / TOS 8** ;font-hex ++ .Screen/addr DEO2
	#01 .Screen/sprite DEO

	( draw octave )
	OVR2 #000b ++ .Screen/x DEO2
	DUP2 #0014 ++ .Screen/y DEO2
	( get pitch ) STHkr TOS ;pad-pitch ++ LDA
	#0c MOD TOS 8** ;font-notes ++ .Screen/addr DEO2
	#01 .Screen/sprite DEO

	POP2 POP2

	POPr POPr

RTN

@draw-octave ( -- )
	
	.oct-view/x1 LDZ2 .oct-view/y1 LDZ2

	OVR2 OVR2 ;keys-left-icns #01 .piano/last LDZ #00 = + ;draw-key JSR2
	OVR2 #0008 ++ OVR2 ;keys-middle-icns #01 .piano/last LDZ #01 = + ;draw-key JSR2
	OVR2 #0010 ++ OVR2 ;keys-right-icns #01 .piano/last LDZ #02 = + ;draw-key JSR2
	OVR2 #0018 ++ OVR2 ;keys-left-icns #01 .piano/last LDZ #03 = + ;draw-key JSR2
	OVR2 #0020 ++ OVR2 ;keys-middle-icns #01 .piano/last LDZ #04 = + ;draw-key JSR2
	OVR2 #0028 ++ OVR2 ;keys-middle-icns #01 .piano/last LDZ #05 = + ;draw-key JSR2
	SWP2 #0030 ++ SWP2 ;keys-right-icns #01 .piano/last LDZ #06 = + ;draw-key JSR2

	.oct-view/x1 LDZ2 #0040 ++ .Screen/x DEO2

	;arrow-icns .Screen/addr DEO2
	.oct-view/y1 LDZ2 .Screen/y DEO2
	#01 .Screen/sprite DEO

	;arrow-icns #0008 ++ .Screen/addr DEO2
	.oct-view/y1 LDZ2 #0010 ++ .Screen/y DEO2
	#01 .Screen/sprite DEO

	;font-hex .piano/octave LDZ #03 + #00 SWP 8** ++ .Screen/addr DEO2
	.oct-view/y1 LDZ2 #0008 ++ .Screen/y DEO2
	#03 .Screen/sprite DEO

RTN

@draw-monitor ( x* y* stereo -- )

	STH 

	.Screen/y DEO2
	.Screen/x DEO2

	#00 #0f
	&loop
		OVR #10 SWP - STHkr #0f AND < INC .Screen/pixel DEO
		.Screen/x DEI2 #0002 ++ .Screen/x DEO2
		OVR #10 SWP - STHkr #04 SFT < INC .Screen/pixel DEO
		.Screen/x DEI2 #0002 -- .Screen/x DEO2
		.Screen/y DEI2 #0002 ++ .Screen/y DEO2
		( incr ) SWP INC SWP
		LTHk ,&loop JCN
	POP2

	POPr

RTN

@draw-key ( x* y* addr* color -- )
		
	STH
	.Screen/addr DEO2 
	SWP2 .Screen/x DEO2
	DUP2 #0018 ++
	&loop
		( move ) OVR2 .Screen/y DEO2
		( draw ) STHkr .Screen/sprite DEO
		( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2  
		( incr ) SWP2 #0008 ++ SWP2
		LTH2k ,&loop JCN
	POP2 POP2 
	POPr

RTN

@draw-cursor ( -- )
	
	( clear last cursor )
	;cursor-icn .Screen/addr DEO2 
	.pointer/x LDZ2 .Screen/x DEO2 
	.pointer/y LDZ2 .Screen/y DEO2 
	#40 .Screen/sprite DEO
	( record pointer positions )
	.Mouse/x DEI2 DUP2 .pointer/x STZ2 .Screen/x DEO2 
	.Mouse/y DEI2 DUP2 .pointer/y STZ2 .Screen/y DEO2 
	( colorize on state )
	#43 [ .Mouse/state DEI #00 ! ] - .Screen/sprite DEO

RTN

@draw-knob ( x* y* value -- )

	( load ) STH .Screen/y DEO2  .Screen/x DEO2
	;knob-icns .Screen/addr DEO2 
	( draw ) #01 .Screen/sprite DEO
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2 
	;knob-icns #0008 ++ .Screen/addr DEO2 
	( draw ) #01 .Screen/sprite DEO
	.Screen/y DEI2 #0008 ++ .Screen/y DEO2 
	;knob-icns #0018 ++ .Screen/addr DEO2 
	( draw ) #01 .Screen/sprite DEO
	.Screen/x DEI2 #0008 -- .Screen/x DEO2 
	;knob-icns #0010 ++ .Screen/addr DEO2 
	( draw ) #01 .Screen/sprite DEO
	.Screen/x DEI2 #0004 ++ .Screen/x DEO2
	.Screen/y DEI2 #0008 ++ .Screen/y DEO2
	;font-hex #00 STHkr #30 SFT ++ .Screen/addr DEO2
	( draw ) #01 .Screen/sprite DEO
	.Screen/x DEI2 #0004 -- #00 #00 STHkr ;knob-offsetx ++ LDA ++ .Screen/x DEO2
	.Screen/y DEI2 #0010 -- #00 #00 STHr ;knob-offsety ++ LDA ++ .Screen/y DEO2
	;knob-icns #0020 ++ .Screen/addr DEO2
	( draw ) #05 .Screen/sprite DEO

RTN

@draw-switch ( x* y* value -- )
	
	STH .Screen/y DEO2 .Screen/x DEO2
	STHkr #50 SFT #00 SWP ;switch-icns ++
	DUP2 .Screen/addr DEO2
	( draw ) #01 STHkr + .Screen/sprite DEO
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	DUP2 #0008 ++ .Screen/addr DEO2
	( draw ) #01 STHkr + .Screen/sprite DEO
	.Screen/y DEI2 #0008 ++ .Screen/y DEO2
	.Screen/x DEI2 #0008 -- .Screen/x DEO2
	DUP2 #0010 ++ .Screen/addr DEO2
	( draw ) #01 STHkr + .Screen/sprite DEO
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	#0018 ++ .Screen/addr DEO2
	( draw ) #01 STHr + .Screen/sprite DEO

RTN

@line-rect ( x1* y1* x2* y2* color -- )

	( load ) .color STZ STH2k .rect/y2 STZ2 .rect/x2 STZ2 STH2k .rect/y1 STZ2 .rect/x1 STZ2
	STH2r INC2 STH2r
	&ver
		( save ) OVR2 .Screen/y DEO2
		( draw ) .rect/x1 LDZ2 .Screen/x DEO2 .color LDZ DUP .Screen/pixel DEO
		( draw ) .rect/x2 LDZ2 .Screen/x DEO2 .Screen/pixel DEO
		( incr ) SWP2 INC2 SWP2
		OVR2 OVR2 LTS2 ,&ver JCN
	POP2 POP2
	.rect/x1 LDZ2 INC2 .rect/x2 LDZ2 #0001 --
	&hor
		( save ) OVR2 .Screen/x DEO2
		( draw ) .rect/y1 LDZ2 .Screen/y DEO2 .color LDZ DUP .Screen/pixel DEO
		( draw ) .rect/y2 LDZ2 .Screen/y DEO2 .Screen/pixel DEO
		( incr ) SWP2 INC2 SWP2
		OVR2 OVR2 INC2 LTS2 ,&hor JCN
	POP2 POP2

RTN

@draw-label ( x* y* addr* color -- )
	
	( load ) STH STH2 .Screen/y DEO2 .Screen/x DEO2
	STH2r
	&loop
		LDAk #00 SWP #0030 -- 8** ;font-num-uc ++ .Screen/addr DEO2 
		( draw ) STHkr .Screen/sprite DEO
		( incr ) INC2
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		LDAk #00 ! ,&loop JCN
	POP2
	POPr

RTN

@fill-rect ( x1* y1* x2* y2* color -- )
	
	.color STZ
	( x1 x2 y1 y2 ) ROT2 SWP2
	&ver
		( save ) OVR2 .Screen/y DEO2
		STH2 STH2 OVR2 OVR2
		&hor
			( save ) OVR2 .Screen/x DEO2
			( draw ) .color LDZ .Screen/pixel DEO
			( incr ) SWP2 INC2 SWP2
			OVR2 OVR2 LTS2 ,&hor JCN
		POP2 POP2 STH2r STH2r
		( incr ) SWP2 INC2 SWP2
		OVR2 OVR2 LTS2 ,&ver JCN
	POP2 POP2 POP2 POP2

RTN

@pad-name [
	"PAD1 $1 "SYN1 $1 "SYN2 $1 "CYM1 $1 ( short )
	"HHAT $1 "OHAT $1 "CHAT $1 "RIDE $1
	"SID1 $1 "SNR1 $1 "SNR2 $1 "SID2 $1 ( long )
	"BDR1 $1 "KCK1 $1 "KCK2 $1 "SUB1 $1 ]

@pad-addr [
	6000 6400 6800 6c00
	7000 7800 8000 8800
	9000 9800 a000 a800
	b000 c000 d000 e000 $2 ]

@pad-pitch [
	30 30 30 30
	30 30 30 30
	30 30 30 30
	30 30 30 30 ]

@pad-path [
	"projects/sounds/pad1.pcm $1 "projects/sounds/syn1.pcm $1 "projects/sounds/syn2.pcm $1 "projects/sounds/pad2.pcm $1
	"projects/sounds/hhat.pcm $1 "projects/sounds/ohat.pcm $1 "projects/sounds/chat.pcm $1 "projects/sounds/ride.pcm $1
	"projects/sounds/sid1.pcm $1 "projects/sounds/snr1.pcm $1 "projects/sounds/snr2.pcm $1 "projects/sounds/sid2.pcm $1
	"projects/sounds/bdr1.pcm $1 "projects/sounds/kck1.pcm $1 "projects/sounds/kck2.pcm $1 "projects/sounds/sub1.pcm $1 ]

@notes [
	3c 3e 40 41 43 45 47
	48 4a 4c 4d 4f 51 53 ]

@cursor-icn [ 
	80c0 e0f0 f8e0 1000 ]

@keys-left-icns [
	7c7c 7c7c 7c7c 7c7c
	7c7c 7c7c 7c7c 7e7f
	7f7f 7f7f 7f7f 3e00 ]

@keys-middle-icns [
	1c1c 1c1c 1c1c 1c1c
	1c1c 1c1c 1c1c 3e7f
	7f7f 7f7f 7f7f 3e00 ]

@keys-right-icns [
	1f1f 1f1f 1f1f 1f1f
	1f1f 1f1f 1f1f 3f7f
	7f7f 7f7f 7f7f 3e00 ]

@arrow-icns [
	0010 387c fe10 1000
	0010 1010 fe7c 3810 ]

@switch-icns [
	001f 2040 4040 4040
	00f8 0402 0202 0202
	404f 5f5f 4f20 1f00
	02f2 fafa f204 f800
	001f 204f 5f5f 4f40
	00f8 04f2 fafa f202
	4040 4040 4020 1f00
	0202 0202 0204 f800 ]

@knob-icns [
	0003 0c10 2020 4040
	00c0 3008 0404 0202
	4040 2020 100c 0300
	0202 0404 0830 c000
	0000 183c 3c18 0000 ]

@knob-offsetx [
	01 00 00 00 00 01 02 03
	05 06 07 08 08 08 08 07 ]

@knob-offsety [
	07 06 05 03 02 01 00 00
	00 00 01 02 03 05 06 07 ]

@font-notes [
	003e 4140 4040 413e 40ae 4100 4040 413e
	007e 4141 4141 417e 40ae 4101 4141 417e
	003e 4140 7c40 413e 003f 4040 7e40 4040
	40af 4000 7e40 4040 003e 4140 5f41 413e
	40ae 4100 5f41 413e 003e 4141 7f41 4141
	40ae 4101 7f41 4141 007e 4141 7e41 417e
]

@font-hex ( 0-F )
[
	007c 8282 8282 827c 0030 1010 1010 1010
	007c 8202 7c80 80fe 007c 8202 1c02 827c
	000c 1424 4484 fe04 00fe 8080 7c02 827c
	007c 8280 fc82 827c 007c 8202 1e02 0202
	007c 8282 7c82 827c 007c 8282 7e02 827c
	007c 8202 7e82 827e 00fc 8282 fc82 82fc
	007c 8280 8080 827c 00fc 8282 8282 82fc
	007c 8280 f080 827c 007c 8280 f080 8080
]

@font-num-uc [
	003e 4141 4141 413e 0018 0808 0808 081c 
	003e 4101 3e40 407f 003e 4101 1f01 413e 
	0011 2141 7f01 0101 007f 4040 7e01 413e 
	003e 4140 7e41 413e 003e 4101 0102 0408 
	003e 4141 3e41 413e 003e 4141 3f01 0102 
	0000 0800 0000 0800 0000 0800 0000 0800 
	0000 0800 0000 0810 0000 0408 1008 0400 
	0000 001c 001c 0000 0000 1008 0408 1000 
	0000 0000 0000 0000 003e 4101 3f41 413f 
	007e 4141 7e41 417e 003e 4140 4040 413e 
	007e 4141 4141 417e 007f 4040 7e40 407f 
	007f 4040 7e40 4040 003e 4140 5e41 413e 
	0041 4141 7f41 4141 0008 0808 0808 0808 
	007f 0101 0101 413e 0041 4244 7844 4241 
	0040 4040 4040 403f 0076 4949 4949 4949 
	005e 6141 4141 4141 003e 4141 4141 413e 
	007e 4141 7e40 4040 003e 4141 4145 423d 
	007e 4141 7e41 4141 003e 4140 3e01 413e 
	007f 0808 0808 0808 0041 4141 4141 433d 
	0041 4141 4122 1408 0049 4949 4949 4976 
	0041 2214 0814 2241 0041 4141 3f01 413e 
	007f 0101 3e40 407f ]
